;NSIS Modern User Interface
;Start Menu Folder Selection Example Script
;Written by Joost Verburg

;--------------------------------
;Include Modern UI

  !include "MUI2.nsh"
  !include "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\FileAssociation.nsh"

;--------------------------------
;General

  ;Name and file
  Name "OpenTX Companion 2.0"
  OutFile "companionInstall_@C9X_VERSION@.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES\OpenTX\Companion 2.0"
  
  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\OpenTX\Companion 2.0" ""
  
  ;Compressor options
  SetCompressor /FINAL /SOLID lzma
  SetCompressorDictSize 64

  ;Request application privileges for Windows Vista
  RequestExecutionLevel admin

;--------------------------------
;Variables

  Var StartMenuFolder

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING
  !define MUI_CUSTOMFUNCTION_GUIINIT removeOld

;--------------------------------
;File Exists Macro

; See http://nsis.sourceforge.net/Check_if_a_file_exists_at_compile_time for documentation
!macro !defineifexist _VAR_NAME _FILE_NAME
	!tempfile _TEMPFILE
	!ifdef NSIS_WIN32_MAKENSIS
		; Windows - cmd.exe
		!system 'if exist "${_FILE_NAME}" echo !define ${_VAR_NAME} > "${_TEMPFILE}"'
	!else
		; Posix - sh
		!system 'if [ -e "${_FILE_NAME}" ]; then echo "!define ${_VAR_NAME}" > "${_TEMPFILE}"; fi'
	!endif
	!include '${_TEMPFILE}'
	!delfile '${_TEMPFILE}'
	!undef _TEMPFILE
!macroend
!define !defineifexist "!insertmacro !defineifexist"

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_LICENSE "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\license.txt"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  
  ;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU" 
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\OpenTX\Companion 2.0" 
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
  
  !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder
  
  !insertmacro MUI_PAGE_INSTFILES
  
  # These indented statements modify settings for MUI_PAGE_FINISH
    !define MUI_FINISHPAGE_NOAUTOCLOSE
    !define MUI_FINISHPAGE_RUN
    !define MUI_FINISHPAGE_RUN_CHECKED
    !define MUI_FINISHPAGE_RUN_TEXT "Launch OpenTX Companion 2.0"
    !define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"
  #  !define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
  #  !define MUI_FINISHPAGE_SHOWREADME $INSTDIR\readme.txt
  !insertmacro MUI_PAGE_FINISH
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"
  !insertmacro MUI_LANGUAGE "French"

;--------------------------------
;Installer Sections

Section "OpenTX Companion" SecDummy

  SetOutPath "$INSTDIR"
  
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\license.txt"
  File "Release\companion.exe"
  File "Release\simulator.exe"
  ${!defineifexist} PTHREAD_WINDIR "C:\Programs\pthreads\Pre-built.2\lib\pthreadVC2.dll"
  !ifdef PTHREAD_WINDIR
  File "C:\Programs\pthreads\Pre-built.2\lib\pthreadVC2.dll"
  !else
  File "@SYSDIR@\system32\pthreadVC2.dll"
  !endif
  File "C:\Programs\xerces-vc\bin\xerces-c_3_1.dll"
  File "C:\Programs\SDL\lib\SDL.dll"
  File "@SYSDIR@\msvcp100.dll"
  File "@SYSDIR@\msvcr100.dll"
  File "@QT_BINARY_DIR@\QtCore4.dll"
  File "@QT_BINARY_DIR@\QtGui4.dll"
  File "@QT_BINARY_DIR@\QtNetwork4.dll"
  File "@QT_BINARY_DIR@\Phonon4.dll"
  File "@QT_BINARY_DIR@\QtXml4.dll"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\avrdude.exe"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\avrdude.conf"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\libusb0.dll"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\dfu-util.exe"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\libusb-1.0.dll"
  
  CreateDirectory "$INSTDIR\phonon_backend"
  SetOutPath "$INSTDIR\phonon_backend"
  File "@QT_BINARY_DIR@\..\plugins\phonon_backend\Phonon_ds94.dll"

  CreateDirectory "$INSTDIR\lang"
  SetOutPath "$INSTDIR\lang"
  File "*.qm"
  
  SetOutPath "$INSTDIR"
  ;Store installation folder
  WriteRegStr HKCU "Software\OpenTX\Companion 2.0" "" $INSTDIR
  
  ;Associate with extentions ,bin and .hex
  ${registerExtension} "$INSTDIR\companion.exe" ".bin" "BIN_File"
  ${registerExtension} "$INSTDIR\companion.exe" ".hex" "HEX_File"
  ${registerExtension} "$INSTDIR\companion.exe" ".companion" "EEPE_File"
  ${registerExtension} "$INSTDIR\companion.exe" ".eepm" "EEPM_File"

  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  
  ;Registry information for add/remove programs
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion 2.0" "DisplayName" "OpenTX Companion 2.0"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion 2.0" "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion 2.0" "DisplayIcon" "$\"$INSTDIR\companion.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion 2.0" "Publisher" "OpenTX"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion 2.0" "URLInfoAbout" "http://www.open-tx.org"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion 2.0" "QuietUninstallString" "$\"$INSTDIR\Uninstall.exe$\" /S"
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    
  ;Create shortcuts
  CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Companion 2.0.lnk" "$INSTDIR\companion.exe"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Firmware Simulator 2.0.lnk" "$INSTDIR\simulator.exe"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall Companion 2.0.lnk" "$INSTDIR\Uninstall.exe"
  
  !insertmacro MUI_STARTMENU_WRITE_END

SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_SecDummy ${LANG_ENGLISH} "Models and settings editor for OpenTX"
  LangString DESC_SecDummy ${LANG_FRENCH} "Editeur de r�glages et mod�les pour OpenTX"
  LangString removeMessage ${LANG_ENGLISH} "An older version of OpenTX Companion is already installed and needs to be removed. \
                                           $\n$\nClick `OK` to remove the previous version or `Cancel` to cancel this upgrade."
  LangString removeMessage ${LANG_FRENCH} "Une ancienne version d'OpenTX Companion est installee et doit etre supprimee. \
                                           $\n$\nCliquer `OK` pour supprimer l'ancienne version ou `Annuler` pour annuler l'installation."

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SecDummy} $(DESC_SecDummy)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END
 
;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;ADD YOUR OWN FILES HERE...  
  
  Delete "$INSTDIR\license.txt"
  Delete "$INSTDIR\companion.exe"
  Delete "$INSTDIR\simulator.exe"
  Delete "$INSTDIR\pthreadVC2.dll"
  Delete "$INSTDIR\xerces-c_3_1.dll"
  Delete "$INSTDIR\SDL.dll"
  Delete "$INSTDIR\msvcp100.dll"
  Delete "$INSTDIR\msvcr100.dll"
  Delete "$INSTDIR\QtCore4.dll"
  Delete "$INSTDIR\QtGui4.dll"
  Delete "$INSTDIR\QtXml4.dll"
  Delete "$INSTDIR\QtNetwork4.dll"
  Delete "$INSTDIR\Phonon4.dll"
  Delete "$INSTDIR\avrdude.exe"
  Delete "$INSTDIR\avrdude.conf"
  Delete "$INSTDIR\libusb0.dll"
  Delete "$INSTDIR\dfu-util.exe"
  Delete "$INSTDIR\libusb-1.0.dll"
  Delete "$INSTDIR\Uninstall.exe"
  
  Delete "$INSTDIR\lang\*.*"
  Delete "$INSTDIR\phonon_backend\*.*"

  RMDir "$INSTDIR\lang"
  RMDir "$INSTDIR\phonon_backend"
  RMDir "$INSTDIR"
  
  ${unregisterExtension} ".bin" "BIN File"
  ${unregisterExtension} ".hex" "HEX File"
  ${unregisterExtension} ".eepm" "EEPE File"
  ${unregisterExtension} ".companion" "EEPM File"
  
  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder
    
  Delete "$SMPROGRAMS\$StartMenuFolder\Companion 2.0.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Firmware Simulator 2.0.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall Companion 2.0.lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"
  
  DeleteRegKey /ifempty HKCU "Software\OpenTX Companion 2.0"
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion 2.0"

SectionEnd

Function LaunchLink
  ExecShell "" "$INSTDIR\companion.exe"
FunctionEnd

Function removeOld
  ReadRegStr $R0 HKLM \
  "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX" \
  "UninstallString"
  StrCmp $R0 "" done
  
  ReadRegStr $R1 HKCU \
  "Software\OpenTX" \
  ""
 
  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \
  "$(removeMessage)" \
  IDOK uninst
  Abort
 
;Run the uninstaller
uninst:
  ClearErrors
  ExecWait '$R0 _?=$R1' ;Prevent concurrent execution
 
  IfErrors no_remove_uninstaller uninstalled
    Delete $R0
    ;You can either use Delete /REBOOTOK in the uninstaller or add some code
    ;here to remove the uninstaller. Use a registry key to check
    ;whether the user has chosen to uninstall. If you are using an uninstaller
    ;components page, make sure all sections are uninstalled.
  no_remove_uninstaller:
    Quit
 
  uninstalled:
    ; Cleanup leftover mess from previous uninstaller
    Delete "$R1\phonon_backend\*.*"
    RMDir "$R1\phonon_backend"
    Delete "$R1\Uninstall.exe"
    RMDir "$R1"
    DeleteRegValue HKCU "Software\OpenTX" ""
    DeleteRegValue HKCU "Software\OpenTX" "Start Menu Folder"
     
done:
 
FunctionEnd
